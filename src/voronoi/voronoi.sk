# See here to understand how this works:
# http://www.skorks.com/2010/10/write-a-function-to-determine-if-a-number-is-a-power-of-2/
def isPowerOfTwo(number int) bool {
    return number != 0 && ((number & (number - 1)) == 0)
}

namespace Voronoi {
    class Options {
        var wrap = false

        # If this is set to a number greater than 0, then when you call
        # Voronoi.drawToOutputCanvas we draw colors to pixels that are
        # within `minSeedDistanceThreshold` of their closest seed
        var minSeedDistanceThreshold = -1.0
    }
}

class Voronoi {
    const _options Options
    var _wham Wham

    var _gridSize int

    var _prepJumpFloodData Wham.Program
    var _jumpFlood Wham.Program
    var _drawJumpFloodData Wham.Program
    var _drawDistanceField Wham.Program
    var _drawShadow Wham.Program
    var _renderTexture Wham.Program

    var _quadBuffer Wham.Buffer
    var _sourceTexture Wham.Texture

    var _destTexture Wham.Texture
    var _seedInputTexture Wham.Texture
    var _tempOutputTexture Wham.Texture
    var _framebuffer Wham.Framebuffer
    var _simulationStepper dynamic


    var _inputCanvas HTMLCanvasElement
    var _outputCanvas HTMLCanvasElement

    def new(canvas HTMLCanvasElement, options Options) {
        if canvas.width != canvas.height {
            throw Error.new("Voronoi canvas parameter must be square. Size is (\(canvas.width), \(canvas.height))")
        }

        if !isPowerOfTwo(canvas.width) {
            throw Error.new("Voronoi canvas size must be a power of two. Size is \(canvas.width)")
        }

        _options = options
        _gridSize = canvas.width
        _outputCanvas = canvas

        _wham = Wham.new(canvas)
        if _wham.ctx == null {
            throw Error.new("Failed to initialize Wham")
        }

        _wham.ctx.disable(_wham.ctx.DEPTH_TEST)

        const ctx = _wham.ctx
        ctx.disable(ctx.DEPTH_TEST)

        _jumpFlood = Wham.Program.new(_wham.ctx, GLSLX_SOURCE_V_COPY_POSITION, GLSLX_SOURCE_F_JUMP_FLOOD)
        _prepJumpFloodData = Wham.Program.new(_wham.ctx, GLSLX_SOURCE_V_COPY_POSITION, GLSLX_SOURCE_F_PREP_FOR_JFA)
        _renderTexture = Wham.Program.new(_wham.ctx, GLSLX_SOURCE_V_COPY_POSITION, GLSLX_SOURCE_F_RENDER_TEXTURE)
        _drawJumpFloodData = Wham.Program.new(_wham.ctx, GLSLX_SOURCE_V_COPY_POSITION, GLSLX_SOURCE_F_DRAW_JUMP_FLOOD_DATA)
        _drawDistanceField = Wham.Program.new(_wham.ctx, GLSLX_SOURCE_V_COPY_POSITION, GLSLX_SOURCE_F_DRAW_DISTANCE_FIELD)
        _drawShadow = Wham.Program.new(_wham.ctx, GLSLX_SOURCE_V_COPY_POSITION, GLSLX_SOURCE_F_DRAW_SHADOW)

        _quadBuffer = Wham.Buffer.new(ctx)
        _quadBuffer.update(Wham.QUAD2, ctx.STATIC_DRAW)

        var opts = Wham.TextureOptions.new(ctx, ctx.RGBA, ctx.REPEAT, ctx.NEAREST, ctx.UNSIGNED_BYTE)
        _sourceTexture = Wham.Texture.new(ctx, opts)
        _sourceTexture.clear(_gridSize, _gridSize)
        _destTexture = Wham.Texture.new(ctx, opts)
        _destTexture.clear(_gridSize, _gridSize)

        opts = Wham.TextureOptions.new(ctx, ctx.RGBA, ctx.REPEAT, ctx.LINEAR, ctx.UNSIGNED_BYTE)
        _tempOutputTexture = Wham.Texture.new(ctx, opts)
        _tempOutputTexture.clear(_gridSize, _gridSize)

        opts = Wham.TextureOptions.new(ctx, ctx.RGBA, ctx.REPEAT, ctx.NEAREST, ctx.UNSIGNED_BYTE)
        _seedInputTexture = Wham.Texture.new(ctx, opts)
        _seedInputTexture.clear(_gridSize, _gridSize)

        _framebuffer = Wham.Framebuffer.create(ctx)
    }

    def _checkFramebufferCompletion {
        if !RELEASE {
            const ctx = _wham.ctx
            var check = ctx.checkFramebufferStatus(ctx.FRAMEBUFFER)
            if check != ctx.FRAMEBUFFER_COMPLETE {
                throw Error.new("Framebuffer completion issue")
            }
        }
    }

    def setSeedsFromCanvas(inputCanvas HTMLCanvasElement) {
        setSeedsFromCanvas(inputCanvas, Float32Array.new([0, 0, 0, 0]))
    }

    def setSeedsFromCanvas(inputCanvas HTMLCanvasElement, backgroundColor Float32Array) {
        _inputCanvas = inputCanvas
        _framebuffer.attach(_sourceTexture)
        _checkFramebufferCompletion

        # Create a texture for the canvas. Bind it to texture position 0.
        _seedInputTexture.set(inputCanvas)
        _seedInputTexture.bind(0)

        _wham.ctx.viewport(0, 0, _gridSize, _gridSize)
        _prepJumpFloodData.use
                          .attrib("quad", _quadBuffer, 2)
                          .uniform("iSeedInputTexture", 0)
                          .uniform("iGridSize", Float32Array.new([_gridSize, _gridSize]))
                          .uniform("iBackgroundColor", backgroundColor)
                          .draw(_wham.ctx.TRIANGLE_STRIP, 4)
    }

    def drawToOutputCanvas {
        # First draw _destTexture into _tempOutputTexture
        _framebuffer.attach(_tempOutputTexture)
        _checkFramebufferCompletion

        # Create a texture for the canvas. Bind it to texture position 0.
        _seedInputTexture.set(_inputCanvas)
        _seedInputTexture.bind(0)

        _destTexture.bind(1)

        _wham.ctx.viewport(0, 0, _gridSize, _gridSize)
        _drawJumpFloodData.use
                          .attrib("quad", _quadBuffer, 2)
                          .uniform("iSeedInputTexture", 0)
                          .uniform("iCellGridTexture", 1)
                          .uniform("iGridSize", Float32Array.new([_gridSize, _gridSize]))
                          .uniform("iMinSeedDistanceThreshold", _options.minSeedDistanceThreshold * Browser.devicePixelRatio)
                          .draw(_wham.ctx.TRIANGLE_STRIP, 4)

        _drawTempTexture
    }

    def drawDistanceFieldToOutputCanvas {
        # First draw _destTexture into _tempOutputTexture
        _framebuffer.attach(_tempOutputTexture)
        _checkFramebufferCompletion
        _destTexture.bind(0)
        _wham.ctx.viewport(0, 0, _gridSize, _gridSize)
        _drawDistanceField
                        .use
                        .attrib("quad", _quadBuffer, 2)
                        .uniform("iCellGridTexture", 0)
                        .uniform("iGridSize", Float32Array.new([_gridSize, _gridSize]))
                        .draw(_wham.ctx.TRIANGLE_STRIP, 4)

        _drawTempTexture
    }

    def drawShadowToOutputCanvas(shadowSpread double) {
        if shadowSpread < 0 || shadowSpread > 1.0 {
            throw Error.new("Invalid shadowSpread \(shadowSpread)")
        }

        # First draw _destTexture into _tempOutputTexture
        _framebuffer.attach(_tempOutputTexture)
        _checkFramebufferCompletion
        _destTexture.bind(0)
        _wham.ctx.viewport(0, 0, _gridSize, _gridSize)
        _drawShadow.use
                    .attrib("quad", _quadBuffer, 2)
                    .uniform("iCellGridTexture", 0)
                    .uniform("iGridSize", Float32Array.new([_gridSize, _gridSize]))
                    .uniform("iShadowSpread", shadowSpread)
                    .draw(_wham.ctx.TRIANGLE_STRIP, 4)
        _drawTempTexture
    }

    # We draw into _tempTexture at a higher resolution than we want on screen
    # and then scale down _tempTexture to antialias
    def _drawTempTexture {
        _wham.defaultFramebuffer.bind
        _checkFramebufferCompletion

        _tempOutputTexture.bind(0)
        _wham.ctx.viewport(0, 0, _outputCanvas.width, _outputCanvas.height)
        _renderTexture.use
                      .attrib("quad", _quadBuffer, 2)
                      .uniform("iTexture", 0)
                      .uniform("iResolution", Float32Array.new([_outputCanvas.width, _outputCanvas.height]))
                      .draw(_wham.ctx.TRIANGLE_STRIP, 4)
    }

    # Useful if you want to compute Voronoi in one step.
    def computeVoronoi {
        var stepSize int = _gridSize / 2
        while stepSize >= 1 {
            _runJumpFloodStep(stepSize)
            stepSize /= 2
        }
        _runJumpFloodStep(2)
        _runJumpFloodStep(1)
    }

    # Use resetVoronoiSteps and stepVoronoi to move foward
    # through iterations of the JFA algorithm. Useful, for
    # example, if you're showing an explanation of the
    # algorithm.
    def resetVoronoiSteps {
        var stepSize double = _gridSize / 2
        _simulationStepper = () => {
            if stepSize >= 1 {
                _runJumpFloodStep(stepSize as int)
                stepSize /= 2
            } else if stepSize < 1 {
                _runJumpFloodStep(2)
                _runJumpFloodStep(1)
            }
        }
    }

    def stepVoronoi {
        _simulationStepper()
    }

    def _runJumpFloodStep(stepSize int) {
        # The current simulation state is always in _sourceTexture
        # and we draw onto _destTexture. So...
        #
        # 1. Bind the _destTexture onto the framebuffer so that
        #    we'll draw onto it.
        #
        # 2. Bind _sourceTexture texture to index 0. This is
        #    passed to the shader by setting the
        #    cellGridTexture uniform below.
        #
        _framebuffer.attach(_destTexture)

        if !RELEASE {
            const gl = _wham.ctx
            var check = gl.checkFramebufferStatus(gl.FRAMEBUFFER)
            if check != gl.FRAMEBUFFER_COMPLETE {
                throw Error.new("Framebuffer completion issue")
            }
        }

        _sourceTexture.bind(0)
        _wham.ctx.viewport(0, 0, _gridSize, _gridSize)
        _jumpFlood
            .use
            .attrib("quad", _quadBuffer, 2)
            .uniform("iCellGridTexture", 0)
            .uniform("iStepSize", stepSize)
            .uniform("iCellGridSize", Float32Array.new([_gridSize, _gridSize]))
            .uniform("iUseTorusDistanceForSeeds", _options.wrap ? 1 : 0)
            .draw(_wham.ctx.TRIANGLE_STRIP, 4)

        self._swapBuffers
    }

    def _swapBuffers {
        # Swap the source and destination textures
        const tmp = _sourceTexture
        _sourceTexture = _destTexture
        _destTexture = tmp
    }
}