@import
namespace console {
    const log dynamic
}

class HTMLWindow {
    var onload dynamic
}

def getCanvas(container HTMLElement, className string) HTMLCanvasElement {
    const gridSize = 256
    const canvas = (container as dynamic).getElementsByClassName(className)[0] as HTMLCanvasElement
    canvas.width = gridSize
    canvas.height = gridSize
    return canvas
}

def createJumpFlood(outputCanvas HTMLCanvasElement) JumpFlood {
    var jumpFlood JumpFlood = null
    try {
        jumpFlood = JumpFlood.new(outputCanvas)
    } catch temp dynamic {
        # Failed to initialize Web-GL. Display error.
        document.getElementById("demo-page").style.display = "none"
        document.getElementById("webgl-error").style.display = null
        throw temp
    }
    return jumpFlood
}

def initializePaintDemo(container HTMLElement) {
    const inputCanvas = getCanvas(container, "input-canvas")
    const outputCanvas = getCanvas(container, "output-canvas")
    const jfa = createJumpFlood(outputCanvas)

    # A CanvasDrawController handles user draw-actions on the input canvas
    # Whenever the user draws, we re-render the voronoi diagram for the input
    const drawController = CanvasDrawController.new(inputCanvas, outputCanvas)
    drawController.onDraw = () => {
        jfa.setSeedsFromCanvas(inputCanvas)
        jfa.computeVoronoi
        jfa.drawToOutputCanvas
    }
}

def initializeFishDemo(container HTMLElement) {
    const inputCanvas = getCanvas(container, "input-canvas")
    const outputCanvas = getCanvas(container, "output-canvas")
    const jfa = createJumpFlood(outputCanvas)

    const fishController = FishGameController.new(inputCanvas, outputCanvas)
    fishController.onDraw = () => {
        jfa.setSeedsFromCanvas(inputCanvas)
        jfa.computeVoronoi
        jfa.drawToOutputCanvas
    }
}

def initializePhotoDemo(container HTMLElement) {
    const inputCanvas = getCanvas(container, "input-canvas")
    const outputCanvas = getCanvas(container, "output-canvas")
    const jfa = createJumpFlood(outputCanvas)

    const photoController = PhotoDemoController.new(inputCanvas, outputCanvas)
    photoController.onDraw = () => {
        jfa.setSeedsFromCanvas(photoController.seedCanvas)
        jfa.computeVoronoi
        jfa.drawToOutputCanvas
    }
}

@entry
def main {
    window.onload = => {
        initializePaintDemo(document.getElementById("paint-demo-container"))
        initializeFishDemo(document.getElementById("fish-demo-container"))
        initializePhotoDemo(document.getElementById("photo-demo-container"))
    }
}
